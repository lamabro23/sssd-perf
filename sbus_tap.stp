global underline = "\033[0;4m"
global normal = "\033[0m"

global is_in = 0
global indent = ""

global curr_req_name = ""
global curr_req_id = -1
global curr_req_type = ""
global curr_target = ""

global send_time = 0
global done_time = 0

global can_stop = 0
global is_id = 0

global results

global method_counter

probe nss_getby_id_send {
    if (nss_getby_id != 0) {
        id = pid()

        printf("---------------------------\n")

        curr_req_id = nss_getby_id
        is_id = 1
        printf("NSS_SEND [%d]: %d\n", id, nss_getby_id);
    }
}

probe nss_getby_name_send {
    if (nss_getby_rawname == "admin@ipa.test"           ||
        nss_getby_rawname == "administrator@samba.test" ||
        nss_getby_rawname == "adminldap@ldap.test") {
        id = pid()

        printf("---------------------------\n")

        curr_req_name = nss_getby_rawname
        printf("NSS_SEND [%d]: %s\n", id, nss_getby_rawname);
    }
}

function convert_id_to_string:string (id:long) %{
    char str[80];
    sprintf(str, "%lld", STAP_ARG_id);
    STAP_RETURN(str);
%}

probe nss_getby_name_done {
    id = pid()
    printf("NSS_DONE [%d]: %s\n", id, nss_getby_rawname);

    if (done_time != 0) {
        printf("The request took: %d\n", done_time)
        if (is_id) {
            is_id = 0
            nss_getby_rawname = convert_id_to_string(curr_req_id)
        }
        results[nss_getby_rawname, nss_getby_req_type, id] <<< done_time
        done_time = 0
        indent = ""
        is_in = 0
    }
}

probe sss_dp_send {
    id = pid()
    modify_indent(1)

    indentation()
#     printf("Increasing indent in dp_send to '%s' with %d\n", indent, is_in)

    if (curr_req_name == opt_name || curr_req_id == opt_id) {
        send_time = gettimeofday_us()
        curr_target = conn_name
    }

    printf("%sSSS_SEND [%d]: %s, %d, %s, %s\n", indent, id, opt_name, opt_id, filter, conn_name)
}

probe sss_dp_done {
    id = pid()

    if (can_stop) {
        can_stop = 0
        done_time = gettimeofday_us() - send_time
    }

    indentation()
    printf("%sSSS_DONE [%d]: %d, %d, %s\n", indent, id, dp_error, error, error_message)

    modify_indent(-1)
#     printf("Reducing indent in dp_done from '%s' with %d\n", indent, is_in)
}

probe sbus_req_call_send {
    if (sbus_method == "getAccountInfo") {
        id = pid()
        modify_indent(1)

        indentation()
#         printf("Increasing indent in call_send to '%s' with %d\n", indent, is_in)
        printf("%sBUS_SEND [%d]: %s, (%s, %s)\n", indent, id, sbus_bus,
                                                  sbus_iface, sbus_method)
    } else {
        method_counter += 1
#         printf("Counting: %d\n", method_counter)
#         printf("Not intrested in method: %s\n", sbus_method)
    }
}

probe sbus_req_call_done {
    if (method_counter <= 0) {
        id = pid()

        if (curr_target == sbus_send) {
            can_stop = 1
        }

        indentation()
        printf("%sBUS_DONE [%d]: %s\n", indent, id, sbus_send)
        modify_indent(-1)
#         printf("Counter is at: %d\n", method_counter)
#         printf("Reducing indent in call_done from '%s' with %d\n", indent, is_in)
    } else {
        method_counter -= 1
#         printf("Skipping...\n")
#         printf("Counting: %d\n", method_counter)
    }
}

probe begin {
    printf("%sStarting%s...\n", underline, normal)
}

probe end {
    printf("\n--------------------------\nThe End...\n")
    foreach ([i, j, k] in results) {
        printf("Requests for %s, %s, %d:\n    Cnt: %d\n    Min: %d\n    Max: %d\n    Avg: %d\n",
               i, j, k, @count(results[i, j, k]), @min(results[i, j, k]),
               @max(results[i, j, k]), @avg(results[i, j, k]))
    }
}

function modify_indent(val) {
    if (is_in <= 0 && val == -1)
        is_in = 0
    else
        is_in += val
}

function indentation() {
    if (is_in != 0) {
        tmp = ""
        for (i = 0; i < is_in; i++) {
            tmp = tmp."  "
        }
        indent = tmp
    }
}
